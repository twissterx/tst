<?xml version="1.0"?>
<!--
# File version: $Revision: 1.1 $,  $Date: 2007-03-18 10:58:08 $
#  Last change: $Author: cnitsa $
-->

<xsl:transform version="1.0"
               xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

<xsl:output method="text" encoding="UTF-8" omit-xml-declaration="yes" indent="no"/>

<xsl:param name="reportTitle" select="'noName'"/>
<xsl:param name="startDate" select="'noDate'" />
<xsl:param name="stopDate" select="'noDate'" />

<xsl:strip-space elements="*"/>

<xsl:template match="/">
    <xsl:apply-templates select="/TimeSlotTracker_Report/dictionary"/>
    <xsl:apply-templates select="/TimeSlotTracker_Report/task"/>
</xsl:template>

<!-- ignoring following elements -->
<xsl:template match="dictionary"/>

<!-- root element -->
<xsl:template match="task">

<xsl:value-of select="/TimeSlotTracker_Report/dictionary/duration-spent-on-all-taks"/>
<xsl:text> </xsl:text>

    <xsl:call-template name="time">
        <xsl:with-param name="allTime" select="./duration/secondsWithChildren"/>
    </xsl:call-template>

<xsl:text>
</xsl:text>

    <xsl:if test="not($startDate='noDate') and not($stopDate='noDate')">
        <xsl:value-of select="/TimeSlotTracker_Report/dictionary/time-period-start"/>
        <xsl:text> </xsl:text>
        <xsl:value-of select="$startDate"/>
        <xsl:text> </xsl:text>
        <xsl:value-of select="/TimeSlotTracker_Report/dictionary/time-period-end"/>
        <xsl:text> </xsl:text>
        <xsl:value-of select="$stopDate"/>
    </xsl:if>

<xsl:text>
  
</xsl:text>
  
    <!-- skipping root task -->
    <xsl:apply-templates mode="child" select="./task" />

</xsl:template>

<!-- showing allTime seconds in type (HH:mm) -->
<xsl:template name="time">
		<xsl:param name="allTime">0</xsl:param>

    <xsl:text>(</xsl:text>
		<xsl:variable name="allHours" select="format-number(floor($allTime div 3600), '00')"/>
 		<xsl:value-of select="$allHours"/>
		<xsl:text>:</xsl:text>
		<xsl:variable name="allMinutes" select="format-number(($allTime - $allHours * 3600) div 60, '00')"/>
		<xsl:value-of select="$allMinutes"/>
 		<xsl:text>)</xsl:text>
</xsl:template>

<!-- showing tasks -->
<xsl:template match="task" mode="child">

     <xsl:if test="not(./duration/secondsWithChildren='0')">
		<xsl:number level="multiple" format="1. " count="task[parent::task]"/>
   	    <xsl:value-of select="name"/>
 		<xsl:text> </xsl:text>

        <xsl:call-template name="time">
			<xsl:with-param name="allTime" select="./duration/secondsWithChildren" />
		</xsl:call-template>

<xsl:text>
</xsl:text>

	<xsl:apply-templates select="./timeslot"/>

<xsl:text>
</xsl:text>

	<xsl:apply-templates select="./task" mode="child"/>

  </xsl:if>
</xsl:template>

<!-- showing timeslots -->
<xsl:template match="timeslot">
 <xsl:if test="not(./duration/seconds='0')">
		<xsl:text>. </xsl:text>
    <xsl:value-of select="./description" />
 		<xsl:text> </xsl:text>
		<xsl:call-template name="time">
			<xsl:with-param name="allTime" select="./duration/seconds" />
		</xsl:call-template>
<xsl:text>
</xsl:text>
 </xsl:if>
</xsl:template>

</xsl:transform>
